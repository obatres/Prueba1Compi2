/*
 * Ejemplo desarrollado por Erick Navarro
 * Blog: e-navarro.blogspot.com
 * Julio - 2018
 */

package analizadores; 
import arbol.Instruccion;
import arbol.Asignacion;
import arbol.Simbolo.*;
import arbol.TablaDeSimbolos;
import java.util.LinkedList;
import arbol.Operacion;
import arbol.Imprimir;
import arbol.Tipo.tipo.*;
import arbol.Tipo;
import java_cup.runtime.*;

parser code 
{:

    public LinkedList<Instruccion> AST;
    /**
     * Método al que se llama automáticamente ante algún error sintactico.
     **/ 
    public void syntax_error(Symbol s){ 
        System.out.println("Error Sintáctico en la Línea " + (s.left) +
        " Columna "+s.right+ ". No se esperaba este componente: " +s.value+"."); 
    } 

    /**
     * Método al que se llama automáticamente ante algún error sintáctico 
     * en el que ya no es posible una recuperación de errores.
     **/ 
    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{ 
        System.out.println("Error síntactico irrecuperable en la Línea " + 
        (s.left)+ " Columna "+s.right+". Componente " + s.value + 
        " no reconocido."); 
    }  
    public LinkedList<Instruccion> getAST() {
        return AST;
    }
:} 

terminal String PTCOMA,PARIZQ,PARDER,CORIZQ,CORDER;
terminal String MAS,MENOS,POR,DIVIDIDO;
terminal Object ENTERO;
terminal String DECIMAL;
terminal String CADENA;
terminal String UMENOS;
terminal String REVALUAR;
terminal String IDENTIFICADOR;
terminal String IGUAL;

non terminal ini;
non terminal LinkedList<Instruccion> instrucciones;
non terminal Instruccion instruccion;
non terminal Operacion expresion;

precedence left MAS,MENOS;
precedence left POR,DIVIDIDO;
precedence right UMENOS; 

start with ini; 

ini::=instrucciones:a
    {:
    parser.AST=a;
    :};

instrucciones ::= 
   instrucciones:a instruccion:b 
    {:
    RESULT= a;
    RESULT.add(b);
    :}
 | instruccion:a 
    {:
    RESULT= new LinkedList<>();
    RESULT.add(a);
    :}; 

instruccion ::= 
   REVALUAR CORIZQ expresion:a CORDER PTCOMA
    {:
    //System.out.println("El valor de la expresión es: "+a);
    RESULT  =  new Imprimir (a);
    :}
 | IDENTIFICADOR:a IGUAL expresion:b PTCOMA 
    {:
    RESULT = new Asignacion(a,b);
    :}
    ;

expresion  ::= 
    MENOS expresion:a                      {://RESULT=a*-1;:}%prec UMENOS     
 |  expresion:a MAS        expresion:b     {:RESULT=new Operacion(Operacion.Tipo_operacion.SUMA,a,b);:}
 |  expresion:a MENOS      expresion:b     {:RESULT=new Operacion(Operacion.Tipo_operacion.RESTA,a,b);:}
 |  expresion:a POR        expresion:b     {:RESULT=new Operacion(Operacion.Tipo_operacion.MULTIPLICACION,a,b);:}
 |  expresion:a DIVIDIDO   expresion:b     {:RESULT=new Operacion(Operacion.Tipo_operacion.DIVISION,a,b);:}
 |  ENTERO:a                               {:RESULT=new Operacion(a,new Tipo(INT),Operacion.Tipo_operacion.NUMERO);:}
 |  DECIMAL:a                              {:RESULT=new Operacion(a,new Tipo(DOUBLE),Operacion.Tipo_operacion.NUMERO);:}
 |  CADENA:a                               {:RESULT=new Operacion(a,new Tipo(STRING),Operacion.Tipo_operacion.NUMERO);:} 
 |  IDENTIFICADOR:a                        {:RESULT=new Operacion(a,Operacion.Tipo_operacion.IDENTIFICADOR);:}
 |  PARIZQ expresion:a PARDER              {:RESULT=a;:}
;
